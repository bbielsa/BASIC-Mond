var printExpression = require("expressions/printexpression.mnd");
var ifExpression = require("expressions/ifexpression.mnd");
var thenExpression = require("expressions/thenexpression.mnd");
var inputExpression = require("expressions/inputexpression.mnd");
var gosubExpression = require("expressions/gosubexpression.mnd");
var gotoExpression = require("expressions/gotoexpression.mnd");
var returnExpression = require("expressions/returnexpression.mnd");
var endExpression = require("expressions/endexpression.mnd");

fun parse(this) {
    var operand = this.parser.parseExpression();

    if(this.token.lexeme == "PRINT") {
        var expressions = [operand];
        while(this.parser.tokenStream.lookAhead(0).type == ";") {
            this.parser.tokenStream.consume();
            expressions.add(this.parser.parseExpression());
        }
        return printExpression.new(expressions);
    }
    else if(this.token.lexeme == "IF") {
        var condition = operand;
        var branch = this.parser.parseExpression();
        return ifExpression.new(condition, branch);
    }
    else if(this.token.lexeme == "THEN") {
        return thenExpression.new(operand);
    }
    else if(this.token.lexeme == "INPUT") {
        return inputExpression.new(operand);
    }
    else if(this.token.lexeme == "GOSUB") {
        return gosubExpression.new(operand);
    }
    else if(this.token.lexeme == "GOTO") {
        return gotoExpression.new(operand);
    }
    else if(this.token.lexeme == "RETURN") {
        return returnExpression.new();
    }
    else if(this.token.lexeme == "END") {
        return endExpression.new();
    }
}

fun new(parser, line, token) {
    return {
        line: line,
        token: token,
        parser: parser,

        parse: parse
    }.enableThis();
}

return {new};
