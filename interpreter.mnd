fun rep(str, times) {
    var s = "";

    for(var i = 0; i < times; i++)
        s += str;

    return s;
}
var random = Random();

fun FART(times) {
    printLn(times);
    return rep("FART ", times);
}

fun INT(number) {
    var str = number + "";
    str = str.split(".")[0];

    return Json.deserialize(str);
}

fun interpret(this) {
    while(this.running)
        this.step();

    if(!this.ignoring)
        printLn("\nInterpreter halted");
}

fun step(this) {
    if(this.line >= this.expressions.length())
    {
        this.running = false;
        return;
    }

    if(this.ignoring) {
        if(this.expressions[this.line].expression.type != "print")
            this.expressions[this.line++].expression.run(this);
        else
            this.line++;
    }
    else
        this.expressions[this.line++].expression.run(this);

}

fun interpretSubroutine(this, start) {
    var line = this.line;
    this.setLine(start);
    this.inSubroutine = true;

    while(this.running && this.inSubroutine) {
        this.step();
    }

    this.line = line;
}

// destination is in BASIC line units, not index of expression in `expressions`
fun setLine(this, destination) {
    var i = 0;
    foreach(var exp in this.expressions) {
        if(exp.line == destination)
        {
            this.line = i;
            return;
        }

        i++;
    }

    // if line doesnt exist stop execution
    this.running = false;
}

fun new(expressions, ignoreOutput) {
    return {
        expressions: expressions,
        line: 0,
        running: true,
        ignoring: ignoreOutput,
        inSubroutine: false,
        variables: {},
        functions: {
            TAB: fun(times) -> rep("  ", times),
            RND: fun(max) -> random.next(max + 1),
            FART: FART,
            INT: INT
        },

        interpret: interpret,
        interpretSubroutine: interpretSubroutine,
        step: step,
        setLine: setLine
    }.enableThis();
}

return {new};
